apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.jayfeng.androiddigest"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 3
        versionName "0.3"
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    // rename the apk with the version name
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.jayfeng.lesscode:lesscode-core:0.1.7'
    compile 'in.srain.cube:ultra-ptr:1.0.9'
    compile('com.octo.android.robospice:robospice-google-http-client:1.4.14') {
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient') //by both name and group
    }
    compile('com.google.http-client:google-http-client-jackson2:1.19.0') {
        exclude(group: 'xpp3', module: 'xpp3') //by both name and group
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient') //by both name and group
        exclude(group: 'junit', module: 'junit') //by both name and group
        exclude(group: 'com.google.android', module: 'android') //by both name and group
    }
    compile 'com.facebook.fresco:fresco:0.2.0'
    compile 'com.umeng:fb:5.1.0'
}
